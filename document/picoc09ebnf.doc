picoc09 の文法(2011-07-11 単項式の文法を修正)

プログラム	::=	関数定義 { 関数定義 } .

関数定義	::=	型名 関数名 仮引数宣言 複文 .

型名		::=	"int" .

関数名		::	識別子 .

仮引数宣言	::=	"(" [ 仮引数並び ] ")" .

仮引数並び	::=	仮引数 { "," 仮引数 } .

仮引数		::=	型名 引数名 .

引数名		::=	識別子 .

複文		::= "{" 変数宣言並び 文並び "}" .

変数宣言並び::=	{ 変数宣言 } .

変数宣言	::=	型名 変数名並び ";" .

変数名並び	::=	変数名 { "," 変数名 } .

変数名		::=	識別子 .

文並び		::=	{ 文 } .

文			::=	式文 | 複文 | 選択文 | 反復文 | リターン文 .

式文		::= [ 式 ] ";" .

選択文		::=	"if" "(" 式 ")" 文 [ "else" 文 ] .

反復文		::=	"while" "(" 式 ")" 文 .

リターン文	::= "return" [ 式 ] ";" .

式			::=	等価式 | 代入式 .  ※LL(1) になっていない

代入式		::= 変数名 "=" 式 .

等価式		::= 関係式 { ( "==" | "!=" ) 関係式 } .

関係式		::=	加算式 { ( "<" | ">" | "<=" | ">=" ) 加算式 } .

加算式		::=	乗算式 { ( "+" | "-" ) 乗算式 } .

乗算式		::=	単項式 { ( "*" | "/" | "%" ) 単項式 } .

単項式		::=	[ 単項演算子 ] 単項式 | 素式 .

素式		::= 変数名 | 関数名 "(" [ 実引数並び ] ")" | 定数 | "(" 式 ")" .

単項演算子	::= "+" | "-" .

実引数並び	::=	式 { "," 式 } .

定数		::=	数字 { 数字 } .

識別子		::=	( 英字 | "_" ) { 英字 | 数字 | "_" } .

数字		::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" .

英字		::= "a" | ... | "z" | "A" | ... | "Z" .
